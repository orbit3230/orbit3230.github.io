---
layout: post
title: "[마이크로프로세서] 3주차 - 어셈블리 언어 (2)"
excerpt: "어셈블리 문법, 분기 명령어, 비교 명령어, 시프트 연산 명령어, 유효주소 저장(포인터) 명령어, 레지스터 간접 어드레싱 모드, 문자 입출력 및 프로그램 종료"

tags:
  - [마이크로프로세서]

toc: true

date: 2024-09-19
last_modified_at: 2024-09-23
---
## 어셈블리 문법
### 1. 분기 명령어
- 프로그램의 분기
  - 직전 실행 결과에 따라 다음 작업을 선택
  - C나 Java 따위에서의 **조건문**에 해당

- 분기 조건 : CCR의  조건 비트들을 활용
  - 직전의 연산 결과가 음수인 지 -> CCR의 N 비트가 `1`인 지
  - 직전이 연산 결과가 0인 지 -> CCR의 Z 비트가 `1`인 지

  <br>
  
- CCR의 비트 설정
  - `N` : 직전 연산 결과의 최상위 비트(MSB)에 해당
  - `Z` : 직전 연산 결과의 모든 비트가 `0`이면 `1`, 그렇지 않으면 `0` (NAND)  

  <br>

- 분기 명령어 : `BR 조건, addr`  
  - e.g., `BR nz, 4000`  
  -> 직전 실행 결과가 음수 or `0`이면, 4000번지로 실행위치 이동  

  <br>

- 절댓값을 구하는 프로그램

```s
        LOAD R1, x
        CMP R1, 0
        BR zp, next
        COPY R2, 0
        SUB R1, R2, R1
        STORE R1, x
next:
        ...
x:    .FILL -23
```

<br>

- 분기 조건 표시

|조건|조건 표시|CCR의 N,Z 비트 조건(회로 설계에 반영)|
|:---:|:---:|---|
|작다|`n`|음수이면 (`N=1`이면)|
|같다|`z`|0이면 (`Z=1`이면)|
|크다|`p`|양수이면(`N=0`&&`Z=0`이면)|
|작거나 같다|`nz`|음수이거나 0이면(`N=1` || `Z=1`)|
|같지 않다|`np`|음수이거나 양수이면(`Z=0`)|
|크거나 같다|`zp`|0이거나 양수이면(`N=0`)|
|무조건(true)|`nzp`|무조건(`always`)|

<br>

### 2. 비교 명령어
- 비교 명령어 : `CMP Rs Rs2`
  - `Rs - Rs2`의 계산 과정에서 CCR 비트들을 갱신
  - 계산 결과는 저장하지 않음
  - e.g., `CMP R1, 99`  
  -> `R1 - 99`의 과정에서 CCR 비트들만  적절히 갱신  

  <br>

- 두 수 중에서 작은 수를 저장하는 프로그램  
`y <- min(x, 15)`  

```s
        LOAD R1, x
        CMP R1, 15
        BR n, else
        COPY R2, 15
        STORE R2, y
        BR nzp, addr
else:
        COPY R2, R1
        STORE R2, y
addr:
        ...
```

<br>

### 3. 시프트 연산 명령어
- 시프트 연산 명령어
  - `LSL Rd, Rs, n` : Logical Shift Left
  - `ASL Rd, Rs, n` : Arithmetic Shift Left
  - `LSR Rd, Rs, n` : Logical Shift Right
  - `ASR Rd, Rs, n` : Arithmetic Shift Right  

  <br>

- C언어와 시프트 연산

|C 언어 표현|어셈블리 언어 표현|
|---|---|
|`int x;`<br>`unsigned y;`|`x: .BLOCK 1`<br>`y: .BLOCK 1`|
|`x = (x >> 1);`|`LOAD R0, x`<br>`ASR R0, R0, 1`<br>`STORE R0, x`|
|`y = (y >> 2);`|`LOAD R0, y`<br>`LSR R0, R0, 2`<br>`STORE R0, y`|
|`x = (x << 3);`|`LOAD R0, x`<br>`ASL R0, R0, 3`<br>`STORE R0, x`|
|`y = (y << 4);`|`LOAD R0, y`<br>`LSL R0, R0, 4`<br>`STORE R0, y`|

<br>

- 시프트 연산을 활용한 곱하기 `10` 의 처리
  - `value x 10` = `value x (8 + 2)` = `(value x 8) + (value x 2)`  

  ```s
        .ORIGIN 0x2000
        LOAD R1, value
        LSL R2, R1, 3
        LSL R3, R1, 1
        ADD R1, R2, R3
        STORE R1, result
  value: .FILL 123456
  result: .BLOCK 1
  ```

  <br>

- 정수를 8로 나눈 몫과 나머지 구하기

```s
        .ORIGIN 0x2000
        LOAD R1, value
        ASR R2, R1, 3
        STORE R2, quotient
        AND R2, R1, 0x07
        STORE R2, remain
value: .FILL 0x12345
quotient: .BLOCK 1
remain: .BLOCK 1
```

<br>

### 4. 유효주소 저장 명령어
- 유효주소 저장 명령어 : LEA (Load Effective Address)
  - 특정 주소를 저장해두고 이후에 계속해서 사용할 때 쓰인다. (포인터)
  - 문법 : `LEA R1, addr`  
  ![lea_visualize][def]  

  <br>

- `LEA`와 `LOAD`의 비교

```s
        .ORIGIN 0x2000
        LOAD R1, value
        LEA R2, value
value: .FILL 0x1234
```

- `LOAD R1, value` : value의 값을 R1에 저장
- `LEA R2, value` : value에 해당하는 주소를 R2에 저장  

<br>

### 5. 레지스터 간접 어드레싱 모드
- **직접 어드레싱 모드** : 메모리 주소를 수치나 레이블로 직접 지정하는 방식  
  - `LOAD R1, 5000` : `R1 <- M[5000]`

- **간접 어드레싱 모드** : 지정한 레지스터의 값에 **offset**를 더한 것을 메모리 주소로 사용하는 방식
  - `LDR R1, R0, 3` : `R1 <- M[R0+3]`

  <br>

- 간접 어드레싱 명령어  

|이름|어셈블리언어 표현|의미|
|---|---|---|
|Load Register Indirect|`LDR Rd, Ra, offset`|`Rd` <- 메모리의 `Ra` + `offset` 번지 내용|
|Store Register Indirect|`STR Rs, Ra, offset`|메모리의 `Ra` + `offset` 번지 <- `Rs` 내용|
|Branch Register Indirect|`BRR nzp, Ra, offset`|조건에 따라 `Ra` + `offset` 번지로 다음 실행 위치를 이동|  

<br>

- LDR 명령어 시각화  
![LDR_visualization](https://i.imgur.com/FdS2kc4.png)  

- STR 명령어 시각화  
![STR_visualization](https://i.imgur.com/wzlnrVO.png)  

<br>

- 레지스터 간접 어드레싱 모드의 활용
  - 메모리 주소가 고정되지 않은 경우 ▽  
  - (1) ***배열의 처리***
    - 배열 `A`의 `i`번째 변수 접근
    - `A`의 주소에 `i`값을 offset으로 더한 값을 레지스터에 저장 후 `LDR` 또는 `STR`
  - (2) **지역변수 접근**
    - 지역변수는 스택에 assign 되므로, 스택 부분의 주소를 기준으로 `LDR` 또는 `STR`  
  - (3) **포인터의 처리**  

  <br>

- C언어의 배열

|C 언어 표현|어셈블리 언어 표현|
|---|---|
|`int a[100];`<br>`int x;`<br>`int i = 7;`|`a: .BLOCK 100`<br>`x: .BLOCK 1`<br>`i: .FILL 7`|
|`a[2] = 22;`|`COPY R1, 22`<br>`LEA R0 a`<br>`STR R1, R0, 2`|
|`x = a[3];`|`LEA R0, a`<br>`LDR R1, R0, 3`<br>`STORE R1, x`|
|`a[i] = 44;`|`LEA R0, a`<br>`LOAD R1, i`<br>`ADD R0, R0, R1`<br>`COPY R1, 44`<br>`STR R1, R0, 0`|

<br>

- Example - **배열**에 26개의 ASCII 문자를 넣는 프로그램  

```s
    .ORIGIN 0x2000

    LEA R1, table
    COPY R2, 'A'
    COPY R0, 26
loop: STR R2, R1, 0
    ADD R1, R1, 1
    ADD R2, R2, 1
    SUB R0, R0, 1
    BR p, loop

table: .BLOCK 26
```

<br>

### 6. 문자 입출력 및 프로그램 종료
- 입출력, 프로세스 생성 등은 **운영체제의 기능을 호출**해야 한다.  

- `SWI`(SoftWare Interrupt) 명령어
  - **운영체제 기능을 호출할 때 사용**한다.  
  - `SWI` 명령어를 실행하면 **운영체제 모드로 바뀐다**.  
  - **특정 기능마다 지정된 번호**로 호출한다. (like 리모컨)
  - 기능별 번호 지정은 운영체제 Master가 결정한다.  
  - 운영체제 모드에서 **복귀하는 명령어** -> `RTI`(ReTurn Interrupt)  

  <br>

- Example  

|이름|어셈블리언어 표현|의미|
|---|---|---|
|문자 읽기|`SWI 0`|키보드에서 문자 1개 입력 받아 `R0`에 저장|
|문자 쓰기|`SWI 1`|`R0`에 저장된 문자 1개 출력 |
|문자열 읽기|`SWI 2`|공백으로 구분되는 단어를 입력 받아 `R0`가 가리키는 메모리 주소부터 차례로 저장.<br>문자열의 끝은 `0`(NULL)|
|문자열 쓰기|`SWI 3`|`R0`가 가리키는 메모리 주소부터 저장된 문자열(단어)을 출력.<br>문자열의 끝은 `0`(NULL)|
|에코모드* 설정|`SWI 4`|키보드 입력 시 에코모드 설정.<br>`R0 = 0`이면 에코 X, `R0 = 1`이면 에코 O<br>(default : 에코 O)|
|프로그램 종료|`SWI 255`|실행중인 프로그램의 종료|

<sub> 에코모드* : 키보드 입력을 화면에 출력해주지는 않으나, 운영체제는 입력을 처리하는 모드. (e.g., PW 입력)</sub>  

<br>

### 7. 프로그램(응용)
- 간단한 369 게임

```s
    .ORIGIN 0x2000
loop: SWI 0
    CMP R0, '3'
    BR z, match
    CMP R0, '6'
    BR z, match
    CMP R0, '9'
    BR np, print
match: COPY R0, '*'
print: SWI 1
    BR nzp, loop
```

- 데이터 합 구하기 (16bit offset 초과 주소)

```s
.ORIGIN 0x2000
    ...
    COPY R0, 0
    LOAD R1, addr
    COPY R2, 1000
loop: LDR R3, R1, 0
    ADD R0, R0, R3
    ADD R1, R1, 1
    SUB R2, R2, 1
    BR p, loop
    STORE R0, sum
    SWI 255

addr: .FILL data
sum:  .BLOCK 1
bigA: .BLOCK 0x50000
data: .BLOCK 1000
```

<br>
<br>
<br>
<br>
<details>
<summary>주의사항</summary>
<div markdown="1">  

이 포스팅은 강원대학교 김용석 교수님의 마이크로프로세서 수업을 들으며 내용을 정리 한 것입니다.  
수업 내용에 대한 저작권은 교수님께 있으니,  
다른 곳으로의 무분별한 내용 복사를 자제해 주세요.  

</div>
</details>

[def]: https://i.imgur.com/wzHgvt6.png